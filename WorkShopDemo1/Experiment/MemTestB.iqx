
-----------------------------------------------------------------
                       Timed Experiment
-----------------------------------------------------------------
This is a very simple memory experiment.
Participants study 6 scene images in a random order then they are tested on the 6 studied images along with 6 new images.

-------------------------------------------
        Encoding   Instructions
-------------------------------------------
<instruct>
/ fontstyle = ("Arial", 24pt, true)
/ nextlabel = "Press spacebar to continue"
/ lastlabel =  "Press spacebar to start the experiment"
/ nextkey = (" ")
</instruct>

<page enc_inst1>
^^^^^^^This experiment has 2 parts. 
^^In the first part, you will see many images of scenes. 
^^Please indicated whether each image is of an indoors or an outdoors scene
</page>

<page enc_inst2>
^^^^^Use the 'j' key to indicate indoors and the 'k' key to indicate outdoors
^^You have 2 seconds to make each decision 
</page>


-------------------------------------------
        Encoding Block
-------------------------------------------


start with the elements that will go into a trial:

< values>
/stimulus= "."
</values>

images that will be presented
< item enc_scene>
/ 1 = "Stimuli/1007.jpg"
/ 2 = "Stimuli/1008.jpg"
/ 3 = "Stimuli/1009.jpg"
/ 4 = "Stimuli/1010.jpg"
/ 5 = "Stimuli/1011.jpg"
/ 6 = "Stimuli/1012.jpg"
</item>


< picture enc_scene>
/ items = enc_scene
/ position = (50%,40%)
/ size = (500px,500px)
/ select = noreplace 
</picture>

Response options
< text indoors>
/ items = ("j = indoors")
/ fontstyle = ("Arial", 24pt, true)
/ position = (25%,80%)
</text>

< text outdoors>
/ items = ("k = outdoors")
/ fontstyle = ("Arial", 24pt, true)
/ position = (75%,80%)
</text>

< text dummy>
/ items = ("x")
/ color = white
/ position = (0,0)
</text>

Fixation cross
< text fixation>
/ items = ("+")
/ fontstyle = ("Arial", 60pt, true)
/ position = (50%,50%)
</text>

Now build items up into trials
<trial enc_scene>
/ stimulustimes = [0=fixation; 1000=enc_scene, indoors, outdoors; 3000=dummy]
/ validresponse = ("j","k")
/ trialduration = 3000
/ beginresponsetime = 1000
/ responseinterrupt = frames
/ ontrialbegin = [ values.stimulus= picture.enc_scene.currentitem]
</trial>


Then combine into ecoding block				
<block Encoding>
/ preinstructions = (enc_inst1, enc_inst2)
/ trials = [1-6=noreplace(enc_scene)]
</block>



-------------------------------------------
        Retrieval   Instructions
-------------------------------------------
<page ret_inst1>
^^^^^^^Great Work!
^^In the second part, you will see many images of scenes. 
^^Some of these images will be 'old' -- an image that you saw in the first part of the experiment.
^^Some of these images will be 'new' -- an image that you haven't seen yet.
</page>

<page ret_inst2>
^^^^^Your job is to indicate whether each image is 'old' or 'new'.
^^Use the 'j' key to indicate old.
^^Use the 'k' key to indicate new.
^^You have 2 seconds to make each decision 
</page>


-------------------------------------------
        Retrieval Block
-------------------------------------------


start with the elements that will go into a trial:

images that will be presented
< item new_scene>
/ 1 = "Stimuli/1001.jpg"
/ 2 = "Stimuli/1002.jpg"
/ 3 = "Stimuli/1003.jpg"
/ 4 = "Stimuli/1004.jpg"
/ 5 = "Stimuli/1005.jpg"
/ 6 = "Stimuli/1006.jpg"
</item>

< picture old_scene>
/ items = enc_scene
/ position = (50%,40%)
/ size = (500px,500px)
/ select = noreplace 
</picture>

< picture new_scene>
/ items = new_scene
/ position = (50%,40%)
/ size = (500px,500px)
/ select = noreplace 
</picture>

Response options
< text old>
/ items = ("j = old")
/ fontstyle = ("Arial", 24pt, true)
/ position = (25%,80%)
</text>

< text new>
/ items = ("k = new")
/ fontstyle = ("Arial", 24pt, true)
/ position = (75%,80%)
</text>

< text correctFB>
/ items = ("Correct!")
/ fontstyle = ("Arial", 24pt, true)
/ position = (50%,50%)
</text>

Now build items up into trials
<trial old_scene>
/ stimulustimes = [0=fixation; 999=old_scene, old, new; 2999=dummy]
/ validresponse = ("j","k")
/ trialduration = 3000
/ beginresponsetime = 999
/ correctresponse = ("j")
/ responseinterrupt = trial
/ correctmessage = true(correctFB,1000)
/ ontrialbegin = [ values.stimulus= picture.old_scene.currentitem]
</trial>

<trial new_scene>
/ stimulustimes = [0=fixation; 1000=new_scene, old, new; 3000=dummy]
/ validresponse = ("j","k")
/ trialduration = 3000
/ beginresponsetime = 1000
/ correctresponse = ("k")
/ responseinterrupt = trial
/ correctmessage = true(correctFB,1000)
/ ontrialbegin = [ values.stimulus= picture.new_scene.currentitem]
</trial>


Then combine into retrieval block
<block Retrieval>
/ preinstructions = (ret_inst1, ret_inst2)
/ trials = [1-12=noreplace(new_scene, old_scene)]
</block>

<data>
/ columns=(group, subject, blockcode, trialcode, trialnum, values.stimulus, response, correct, latency)
</data>

<expt MemTestA>
/ blocks = [1=Encoding; 2=Retrieval]
</expt>





